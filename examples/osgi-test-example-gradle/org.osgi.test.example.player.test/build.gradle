import aQute.bnd.gradle.Resolve
import aQute.bnd.gradle.TestOSGi

plugins {
	id("org.osgi.test.example.java-conventions")
}

dependencies {
	compileOnly("org.osgi:osgi.core:8.0.0")
	compileOnly("org.osgi:org.osgi.namespace.service:1.0.0")
	implementation(project(":org.osgi.test.example.api"))
	implementation(platform("org.junit:junit-bom:${junit_jupiter_version}"))
	implementation("org.junit.jupiter:junit-jupiter")
	implementation("org.assertj:assertj-core:${assertj_version}")
	implementation("org.mockito:mockito-core:${mockito_version}")
	implementation(platform("org.osgi:org.osgi.test.bom:${osgi_test_version}"))
	implementation("org.osgi:org.osgi.test.junit5")
	implementation("org.osgi:org.osgi.test.assertj.framework")
	// Dependencies for the Resolve and TestOSGi tasks
	runtimeOnly(project(":org.osgi.test.example.player.impl"))
	runtimeOnly("org.junit.platform:junit-platform-launcher")
	runtimeOnly("org.apache.felix:org.apache.felix.scr:2.2.6")
	runtimeOnly("ch.qos.logback:logback-classic:1.3.4")
	runtimeOnly("org.apache.felix:org.apache.felix.logback:1.0.6")
	runtimeOnly("org.eclipse.platform:org.eclipse.osgi:3.18.500")
	runtimeOnly("org.osgi:org.osgi.service.component:1.5.1")
	runtimeOnly("org.osgi:org.osgi.util.function:1.2.0")
	runtimeOnly("org.osgi:org.osgi.util.promise:1.3.0")
}

description = "OSGi Testing Example Player Test"

tasks.named(JavaPlugin.JAR_TASK_NAME) {
	bundle {
		bnd("Test-Cases: \${classes;HIERARCHY_INDIRECTLY_ANNOTATED;org.junit.platform.commons.annotation.Testable;CONCRETE}")
	}
}

def resolveTask = tasks.register("resolve", Resolve) {
	description = "Resolve test.bndrun"
	group = JavaBasePlugin.VERIFICATION_GROUP
	bndrun = file("test.bndrun")
	outputBndrun = layout.buildDirectory.file("test.bndrun")
}

def testOSGiTask = tasks.register("testOSGi", TestOSGi) {
	description = "OSGi Test test.bndrun"
	group = JavaBasePlugin.VERIFICATION_GROUP
	bndrun = resolveTask.flatMap { it.outputBndrun }
}

tasks.named(LifecycleBasePlugin.CHECK_TASK_NAME) {
	dependsOn(testOSGiTask)
}
